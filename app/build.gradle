apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

if (project.hasProperty("geotaur.signing")
        && new File(project.property("geotaur.signing") + ".gradle").exists()) {
    apply from: project.property("geotaur.signing") + ".gradle";
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "net.maiatoday.geotaur"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        geo {
            applicationId "net.maiatoday.geotaur"
            versionName "0.1-geo"
        }
        neo {
            applicationId "net.maiatoday.neotaur"
            versionName "0.1-neo"
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Dependency injection
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    apt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    provided "javax.annotation:jsr250-api:$rootProject.javaxAnnotationsVersion"

    // Support libs
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"

    // Testing
    testCompile "junit:junit:$rootProject.junitVersion"

    // Firebase
    compile "com.google.firebase:firebase-core:$rootProject.firebaseVersion"
    compile "com.google.firebase:firebase-crash:$rootProject.firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$rootProject.firebaseVersion"
    compile "com.google.firebase:firebase-config:$rootProject.firebaseVersion"
    compile project(path: ':quip')

    // Location
    compile "com.google.android.gms:play-services-location:$rootProject.ext.playServicesVersion"
    compile "com.google.android.gms:play-services-gcm:$rootProject.ext.playServicesVersion"
    neoCompile "com.google.android.gms:play-services-contextmanager:$rootProject.ext.playServicesVersion"


}

apply plugin: 'com.google.gms.google-services'
